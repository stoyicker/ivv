apply from: "dependencies.gradle"

buildscript {
  repositories {
    google()
    jcenter()
    maven { url "https://plugins.gradle.org/m2/" }
  }
  rootProject.ext.kotlinVersion = "1.3.61"
  dependencies {
    classpath "com.android.tools.build:gradle:3.5.3"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$rootProject.ext.kotlinVersion"
    classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.18"
    classpath "org.jmailen.gradle:kotlinter-gradle:2.1.3"
    classpath "com.github.ben-manes:gradle-versions-plugin:0.27.0"
  }
}

rootProject.ext {
  androidCompileSdkVersion = 29
  androidMinSdkVersion = 21
  androidTargetSdkVersion = 29
  androidBuildToolsVersion = "29.0.2"
  androidVersionCode = 1
  androidVersionName = String.valueOf(androidVersionCode)
  androidTestInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
  androidApplicationIdBase = "org.jorge.test"
  staticAnalysisReportTarget = "$project.rootDir.absolutePath${File.separatorChar}staticAnalysisReport"
  javaVersion = JavaVersion.VERSION_1_8
}

subprojects {
  configurations.all {
    resolutionStrategy {
      forcedModules = [
          "org.jetbrains.kotlin:kotlin-stdlib:$rootProject.ext.kotlinVersion",
          "com.squareup.moshi:moshi-kotlin-codegen:1.9.2",
          "androidx.annotation:annotation:1.1.0",
          // https://github.com/mockk/mockk/issues/281
          "org.objenesis:objenesis:2.6"
      ]
    }
  }
}

apply plugin: "com.github.ben-manes.versions"

dependencyUpdates {
  gradleReleaseChannel = "current"
  doFirst {
    repositories {
      google()
      jcenter()
      mavenCentral()
    }
  }
  rejectVersionIf {
    ["jsr250-api", "lint-gradle", "aapt2"].contains(it.candidate.moduleIdentifier.name ) ||
        it.candidate.version.contains("alpha") ||
        it.candidate.version.contains("beta") ||
        it.candidate.version.contains("rc")
  }
}
