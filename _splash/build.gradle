apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jmailen.kotlinter'
apply plugin: 'org.jetbrains.dokka-android'

android {
    compileSdkVersion rootProject.ext.androidCompileSdkVersion
    buildToolsVersion rootProject.ext.androidBuildToolsVersion
    defaultConfig {
        applicationId "org.jorge.test"
        versionCode rootProject.ext.androidVersionCode
        versionName rootProject.ext.androidVersionName
        minSdkVersion rootProject.ext.androidMinSdkVersion
        targetSdkVersion rootProject.ext.androidTargetSdkVersion
        testInstrumentationRunner rootProject.ext.androidTestInstrumentationRunner
    }
    buildTypes {
        debug {
            proguardFiles 'proguard/rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dexOptions {
        preDexLibraries false
        javaMaxHeapSize "8g"
    }
    lintOptions {
        abortOnError true
        absolutePaths true
        checkAllWarnings true
        htmlOutput = new File("$rootProject.ext.staticAnalysisReportTarget/$project.name/lint.html")
        quiet false
        warningsAsErrors true
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    sourceSets.forEach {
        it.java.srcDirs = ["src/$it.name/kotlin"]
    }
    variantFilter {
        if (it.buildType.name.toLowerCase(Locale.ENGLISH) == 'release') {
            ignore = true
        }
    }
}
repositories {
    jcenter()
    google()
}
dependencies {
    implementation rootProject.ext.implementationDependencies
    implementation rootProject.ext.implementationSplashDependencies
    testImplementation rootProject.ext.testImplementationDependencies
    androidTestImplementation rootProject.ext.androidTestImplementationSplashDependencies
    kapt rootProject.ext.kaptSplashDependencies
}

kapt {
    arguments {
        arg('testaccessors.defaultSupportRestrictTo', 'TESTS')
    }
}
