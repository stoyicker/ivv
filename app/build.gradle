apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jmailen.kotlinter'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.dokka-android'

android {
    compileSdkVersion rootProject.ext.androidCompileSdkVersion
    buildToolsVersion rootProject.ext.androidBuildToolsVersion
    defaultConfig {
        versionCode rootProject.ext.androidVersionCode
        versionName rootProject.ext.androidVersionName
        minSdkVersion rootProject.ext.androidMinSdkVersion
        targetSdkVersion rootProject.ext.androidTargetSdkVersion
        testInstrumentationRunner rootProject.ext.androidTestInstrumentationRunner
    }
    buildTypes {
        debug {
            proguardFiles 'proguard/rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dexOptions {
        preDexLibraries false
        javaMaxHeapSize "8g"
    }
    lintOptions {
        abortOnError true
        absolutePaths true
        checkAllWarnings true
        htmlOutput = new File("$rootProject.ext.staticAnalysisReportTarget/$project.name/lint.html")
        quiet false
        warningsAsErrors true
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    variantFilter {
        if (it.buildType.name.toLowerCase(Locale.ENGLISH) == 'release') {
            ignore = true
        }
    }
}
repositories {
    jcenter()
    google()
}
dependencies {
    implementation project(':data')
    implementation project(':domain')
    rootProject.ext.androidTestCompileAppDependencies.forEach {
        androidTestImplementation(it) {
            exclude group: 'com.google.code.findbugs'
            exclude module: 'support-annotations'
        }
    }
    kapt rootProject.ext.annotationProcessorAppDependencies
    kaptAndroidTest rootProject.ext.annotationProcessorAndroidTestAppDependencies
    implementation rootProject.ext.compileDependencies
    implementation rootProject.ext.compileAppDependencies
    testImplementation rootProject.ext.testCompileDependencies
}
